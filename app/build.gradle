apply plugin: 'com.android.application'

android {

    def BOOLEAN = "boolean"
    def TRUE = "true"
    def FALSE = "false"
    def PRODUCTION = "PRODUCTION"
    def DEVELOP = "DEVELOP"
    def STAGING = "STAGING"

    compileSdkVersion rootProject.ext.mTargetSdkVersion
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "net.analizer.tamboon"
        minSdkVersion rootProject.ext.mMinSdkVersion
        targetSdkVersion rootProject.ext.mTargetSdkVersion
        versionCode rootProject.ext.mVersionCode
        versionName rootProject.ext.mVersionName

//        testInstrumentationRunner "th.co.elevenstreet.app.runners.CustomTestRunner"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            keyAlias 'testkey'
            keyPassword 'test1234'
            storeFile file("../distribution/testkey.jks")
            storePassword 'test1234'
        }
        debug {
            keyAlias 'testkey'
            keyPassword 'test1234'
            storeFile file("../distribution/testkey.jks")
            storePassword 'test1234'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            // signing cert setting
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            versionNameSuffix "-DEBUG"
            signingConfig signingConfigs.debug
        }
    }
    dataBinding {
        enabled = true
    }

    flavorDimensions "mainapp"
    productFlavors {
        dev {
            dimension "mainapp"
            versionName rootProject.ext.mVersionName + "-DEV"
            resValue "string", "app_name", "Tamboon - Dev"
            applicationIdSuffix ".dev"
            buildConfigField BOOLEAN, DEVELOP, TRUE
            buildConfigField BOOLEAN, STAGING, FALSE
            buildConfigField BOOLEAN, PRODUCTION, FALSE
        }
        stage {
            dimension "mainapp"
            versionName rootProject.ext.mVersionName + "-STAGING"
            resValue "string", "app_name", "Tamboon - Stage"
            applicationIdSuffix ".stage"
            buildConfigField BOOLEAN, DEVELOP, FALSE
            buildConfigField BOOLEAN, STAGING, TRUE
            buildConfigField BOOLEAN, PRODUCTION, FALSE
        }
        prod {
            dimension "mainapp"
            resValue "string", "app_name", "Tamboon"
            buildConfigField BOOLEAN, DEVELOP, FALSE
            buildConfigField BOOLEAN, STAGING, FALSE
            buildConfigField BOOLEAN, PRODUCTION, TRUE
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    configurations {
        all*.exclude module: 'servlet-api'
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests {
            includeAndroidResources = true
        }
    }
}
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500" // or whatever number you want
    }
}
android.applicationVariants.all { variant ->
    def aptOutputDir = new File(buildDir, "generated/source/apt/${variant.unitTestVariant.dirName}")
    variant.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
}
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-web:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-intents:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test:runner:1.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('com.android.support.test:rules:1.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'org.mockito:mockito-android:2.7.22'
    androidTestImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:3.4.2'
    testImplementation 'org.robolectric:shadows-play-services:3.4-rc2'
    testImplementation 'org.robolectric:shadows-support-v4:3.4-rc2'
    testImplementation 'org.assertj:assertj-core:2.6.0'
    testImplementation 'org.awaitility:awaitility:3.0.0'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.1'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.1'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.1'
    testImplementation 'org.powermock:powermock-classloading-xstream:1.7.1'
    //    testImplementation group: 'cglib', name: 'cglib', version: '3.2.5'
//    implementation 'com.android.support:support-core-utils:27.0.2'

    /*                              support libs                            */
    implementation 'com.android.support:support-v4:27.0.2'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    /* ---------------------------------------------------------------      */

    /*                              Dagger 2                                */
    implementation 'com.google.dagger:dagger:2.11'
    testImplementation 'com.google.dagger:dagger:2.11'
    androidTestImplementation 'com.google.dagger:dagger:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    testAnnotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    androidTestAnnotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    provided 'javax.annotation:jsr250-api:1.0'
    /* ---------------------------------------------------------------      */

    /*                      Other 3rd party's libs                          */
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'co.omise:omise-android:2.3.0'
    /* ---------------------------------------------------------------      */

    /* ---------------------------------------------------------------  */
    // Leak Canary (Detecting leaks)
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    /* ---------------------------------------------------------------  */

    /*                              RxJava                                  */
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    /* ---------------------------------------------------------------  */

    /*                              NetWork                                 */
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    /* ---------------------------------------------------------------      */

    implementation project(':domainlayer')
    implementation project(':datalayer')
}
